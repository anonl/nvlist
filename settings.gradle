
// Note: Use --include-build flag to override external JAR dependencies with local copies of those libraries.
//   See: https://docs.gradle.org/current/userguide/composite_builds.html#command_line_composite 

rootProject.name = 'nvlist'

def subProjects = [
    'api',
    'core',
    'desktop',
    'android',
    'ios',
    'docs',
    'buildtools',
    'buildgui',
    'langserver',
]

def env = System.getenv()
if (!new File('local.properties').exists() && env['ANDROID_SDK_ROOT'] == null) {
    logger.warn('Android SDK not found, skipping Android compilation')
    subProjects.remove('android');
}

subProjects.each { proj ->
    include "nvlist-$proj"
    project(":nvlist-$proj").projectDir = new File(proj)
}

// includeBuild '../tcommon'
// includeBuild '../gdx-styledtext'
// includeBuild '../gdx-test'
// includeBuild '../luajpp2'
