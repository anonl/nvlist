
configurations { natives }

repositories {
    // Android build tools aren't available in Maven central
    google()
}

android {
    buildToolsVersion '29.0.2'
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [vnResDir]
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    lintOptions {
        textOutput 'stdout'
        textReport true
    }

    if (android.hasProperty('applicationVariants')) {
        android.applicationVariants.all { variant ->
            variant.mergeAssets.doLast {
                // Generate an assets.list file containing all assets files in the APK
                def fileTree = fileTree(dir: outputDir)

                def assetList = ""
                fileTree.visit {
                    assetList <<= it.relativePath
                    if (it.isDirectory()) {
                        // Add a trailing slash to distinguish folders from files
                        assetList <<= "/"
                    }
                    assetList <<= "\n"
                }

                def outputFile = outputDir.file("assets.list").get().asFile
                outputFile.text = assetList

                logger.info("Wrote assets list to {}", outputFile)
            }
        }
    }
}

task assembleDist {
    dependsOn 'assembleRelease'
    dependsOn 'assembleDebug'
}
