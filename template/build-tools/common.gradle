
// Shared Gradle code between ./template/build.gradle and ./build.gradle

configurations {
    buildtools { }
    langserver { }
}

dependencies {
    buildtools 'org.slf4j:jul-to-slf4j:1.7.30'
    buildtools 'ch.qos.logback:logback-classic:1.2.3'
    langserver "nl.weeaboo.vn:nvlist-langserver:$nvlistVersion"
}

// To run against a local prerelease version:
// gradlew --PvnRoot=.. --include-build=../..
task runLanguageServer(type: JavaExec) {
    classpath = files(configurations.langserver)
    main = 'nl.weeaboo.vn.langserver.LangServerMain'
    args (findProperty('args') ?: '')
    workingDir vnRootDir
}

task optimizeResources(type: JavaExec) {
    // Optimizer config is stored in build-res/optimizer.json
    def configFile = new File(buildResDir, 'optimizer.json')
    inputs.file(configFile)

    def outputFolder = new File(buildDir, 'res-optimized')
    outputs.dir(outputFolder)

    // Setup to launch the resource optimizer
    classpath = configurations.buildtools
    main = 'nl.weeaboo.vn.buildtools.optimizer.ResourceOptimizerLauncher'
    args = [vnRootDir, buildToolsDir, configFile, outputFolder]
    workingDir vnRootDir

    doFirst {
        // Delete the output folder first
        delete(outputFolder)
    }
}
