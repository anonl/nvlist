
apply plugin: 'java-library'
apply plugin: 'application'

def vnId = rootProject.name
mainClassName = "nl.weeaboo.vn.desktop.DesktopLauncher"
applicationName = vnId

test {
    workingDir = vnRootDir
}

run {
    workingDir = vnRootDir
    args '--Pdebug=true'
    ignoreExitValue = true
}

tasks.register('runDesktop') {
    dependsOn run
}

eclipse {
    project {
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/template'
    }
}

tasks.register('assembleNvl', Zip) {
    from rootProject.tasks.optimizeResources
    archiveName "${vnId}.nvl"
}
assembleDist.dependsOn assembleNvl

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
   baseName = applicationName
   classifier = null
   version = null
}

apply from: "${buildToolsDir}/common-packr.gradle"

packr {
    resources {
        from(assembleNvl)

        // The resource preloader needs an analytics file to function.
        // Use the accumulated analytics from the developer play-testing as the starting point.
        from("$vnRootDir/save") {
            into 'save'
            include 'analytics.bin'
        }
    }
}
